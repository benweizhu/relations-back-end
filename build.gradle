buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.2.2.RELEASE'
        classpath 'com.bluepapa32:gradle-watch-plugin:0.1.4'
        classpath 'me.zeph:gradle-database-plugin:0.0.1'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'checkstyle'
apply plugin: 'com.bluepapa32.watch'
apply plugin: 'me.zeph.database'

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    mysqlDriver
}

dependencies {
    mysqlDriver 'mysql:mysql-connector-java:5.1.18'

    compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.10'
    compile 'com.google.guava:guava:18.0'
    compile 'org.apache.commons:commons-jexl:2.1.1'
    compile 'mysql:mysql-connector-java:5.1.18'
    compile 'org.apache.tomcat.embed:tomcat-embed-jasper'

    compile "com.mangofactory:swagger-springmvc:1.0.0"
    compile "org.ajar:swagger-spring-mvc-ui:0.4"

    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.hateoas:spring-hateoas:0.16.0.RELEASE'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'javax.servlet:servlet-api:2.5'
    testCompile 'junit:junit:4.11'
    testCompile 'com.jayway.jsonpath:json-path-assert:0.9.1'
}

jar {
    baseName = 'relations-back-end'
    version = '0.1.0'
}

idea.project.ipr {
    withXml { provider ->
        provider.node.component
                .find { it.@name == 'VcsDirectoryMappings' }
                .mapping.@vcs = 'Git'
    }
}

watch {
    java {
        files files('src/main/java')
        tasks 'compileJava'
    }
    resources {
        files fileTree(dir: 'src/main/resources', include: '**/*.xml')
        files fileTree(dir: 'src/main/resources', include: '**/*.properties')
        tasks 'processResources'
    }
}

database {
    url = 'jdbc:mysql://localhost:3306/relations'
    username = 'root'
    password = ''
    driver = 'com.mysql.jdbc.Driver'
    configurationName = 'mysqlDriver'
    sqlFiles = files('config/database/relations_drop.sql',
            'config/database/relations_create.sql',
            'config/database/insert_data.sql')
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}

test.dependsOn createDB